android.libraryVariants.all { variant ->
    if (variant.buildType == 'debug') return;

    task("${variant.name}Javadoc", type: Javadoc, dependsOn: "assemble${variant.name.capitalize()}") {

        source = variant.javaCompile.source
        title = "${project.name} API"
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.linksOffline("http://d.android.com/reference", "${android.sdkDirectory}/docs/reference");

        // First add all of your dependencies to the classpath, then add the android jars
        classpath += files(variant.javaCompile.classpath.files)
        classpath += files(android.getBootClasspath())

        // We're excluding these generated files
        exclude '**/BuildConfig.java'
        exclude '**/R.java'

        failOnError = false
    }

    task("${variant.name}JavadocJar", type: Jar, dependsOn: "${variant.name}Javadoc") {
        group = 'publishing'
        classifier = 'javadoc'
        from tasks["${variant.name}Javadoc"].destinationDir
    }

    // Add the Javadoc jar to the project's artifacts. This will allow us to upload it easily later
    project.artifacts.add("archives", tasks["${variant.name}JavadocJar"]);

    task("${variant.name}SourceJar", type: Jar) {
        group = 'publishing'
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    project.artifacts.add("archives", tasks["${variant.name}SourceJar"]);
}